####
#
# Based on packages found at these URLs
#     https://www.archlinux.org/packages/extra/i686/wxgtk-common/
#     https://github.com/Alexpux/MINGW-packages/tree/master/mingw-w64-wxWidgets
#
#     https://github.com/msys2/msys2/wiki/Porting
#
# Maintainer: Tim Stahlhut <stahta01@gmail.com>
#
####

# Packages that are assumed to be installed when building this package.
#   pacman -S --needed msys2-devel base-devel
# Basic build command
#   makepkg -sLf

_basename=wxWidgets
_sourcedir=${_basename}-git
_wx_basever=3.1
_namesuffix="-git"
pkgbase=${_basename}${_namesuffix}

pkgname=(
  wxmsw${_wx_basever}${_namesuffix}
)
pkgver=3.1.3.0.r66562.c2.g235972bc6e
pkgrel=1
pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
arch=('any')
license=("custom:wxWindows")
url="https://www.wxwidgets.org/"
makedepends=(
  "git"
  "make"
  "liblzma-devel"
)
checkdepends=()
options=('strip' 'staticlibs' 'buildflags')
install=${_basename}-${CARCH}.install
source=(
  ${_sourcedir}::"git+https://github.com/wxWidgets/wxWidgets.git#branch=master"

  "005-wxWidgets-3.1.3-Remove-WX_LIBS_STATIC-from-m4.patch"
  "011-wxWidgets-3.1.2-Enable-wxUSE_GRAPHICS_DIRECT2D.patch"
)
sha256sums=('SKIP'
            '4a4828f0c9cdc638cffde6a30b5dfb14283719acc9e89e19de8ec2d5a80a5aec'
            'bf774c66de7c1a8ef3efad364dcb781038da4ee96b638a0315c9b7a9d4855ae1')

_git_base_commit=

pkgver() {
  cd ${srcdir}/${_sourcedir}
  local _major=$(head -n 34 include/wx/version.h | grep 'wxMAJOR_VERSION' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor1=$(head -n 34 include/wx/version.h | grep 'wxMINOR_VERSION' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor2=$(head -n 34 include/wx/version.h | grep 'wxRELEASE_NUMBER' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor3=$(head -n 34 include/wx/version.h | grep 'wxSUBRELEASE_NUMBER' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  printf "%s.%s.%s.%s.r%s.c%s.g%s" "$_major" "$_minor1" "$_minor2" "$_minor3" $(git rev-list --count ${_git_base_commit}) $(git rev-list --count ${_git_base_commit}..HEAD) $(git rev-parse --short ${_git_base_commit})
}

prepare() {
  cd "${srcdir}/${_sourcedir}"

  git submodule init
  git submodule update

  _git_base_commit=$(git rev-parse HEAD)
  GIT_AM="git am --committer-date-is-author-date"

  # MSys2 fails to set WX_LIBS_STATIC correctly; removed to see if something breaks.
  ${GIT_AM} ${srcdir}/005-wxWidgets-3.1.3-Remove-WX_LIBS_STATIC-from-m4.patch

  ${GIT_AM} ${srcdir}/011-wxWidgets-3.1.2-Enable-wxUSE_GRAPHICS_DIRECT2D.patch
  # ${srcdir}/${_sourcedir}/build/update-setup-h
}

build() {
  ####
  # Configure options added to check for build issues
  #   --disable-precomp-headers
  #   --disable-sys-libs
  #
  # Configure options added to avoid possible future issues
  #   --with-cxx=14
  #   --enable-std_string
  #   --enable-std_iostreams
  #   --enable-std_containers_compat
  #
  # Configure options added to avoid warnings:
  #
  # Configure options known to cause build errors:
  #   --disable-regkey                                compile error
  #
  ####

  [[ -d "${srcdir}"/build-msw-${CARCH}-static ]] && rm -rf "${srcdir}"/build-msw-${CARCH}-static
  mkdir -p "${srcdir}"/build-msw-${CARCH}-static && cd "${srcdir}"/build-msw-${CARCH}-static

  ../${_sourcedir}/configure \
      --prefix=${MSYSTEM_PREFIX} \
      --host=${MSYSTEM_CARCH}-pc-mingw32 \
      --target=${MSYSTEM_CARCH}-pc-mingw32 \
      --build=${MSYSTEM_CARCH}-pc-mingw32 \
      --with-msw \
      --with-cxx=14 \
      --enable-std_string \
      --enable-std_iostreams \
      --enable-std_containers_compat \
      --disable-sys-libs \
      --disable-shared \
      --disable-precomp-headers \
      --enable-iff \
      --enable-permissive \
      --enable-unicode \
      --enable-accessibility \
      --disable-monolithic

  make -j1 #VERBOSE=1

  [[ -d "${srcdir}"/install-msw-${CARCH}-static ]] && rm -rf "${srcdir}"/install-msw-${CARCH}-static
  make -j1 DESTDIR="${srcdir}"/install-msw-${CARCH}-static install

  [[ -d "${srcdir}"/build-msw-${CARCH} ]] && rm -rf "${srcdir}"/build-msw-${CARCH}
  mkdir -p "${srcdir}"/build-msw-${CARCH} && cd "${srcdir}"/build-msw-${CARCH}

  ../${_sourcedir}/configure \
      --prefix=${MSYSTEM_PREFIX} \
      --host=${MSYSTEM_CARCH}-pc-mingw32 \
      --target=${MSYSTEM_CARCH}-pc-mingw32 \
      --build=${MSYSTEM_CARCH}-pc-mingw32 \
      --with-msw \
      --with-cxx=14 \
      --enable-std_string \
      --enable-std_iostreams \
      --enable-std_containers_compat \
      --disable-sys-libs \
      --enable-shared \
      --enable-iff \
      --enable-permissive \
      --enable-unicode \
      --enable-accessibility \
      --disable-monolithic

  make -j1 #VERBOSE=1

  [[ -d "${srcdir}"/install-msw-${CARCH} ]] && rm -rf "${srcdir}"/install-msw-${CARCH}
  make -j1 DESTDIR="${srcdir}"/install-msw-${CARCH} install

}

check() {
  cd "${srcdir}"/build-msw-${CARCH}/samples   && make -k --jobs=1 || true

  cd "${srcdir}"/build-msw-${CARCH}/tests     && make -k --jobs=1 || true
}

_package_wxmsw() {
  pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (cygwin)"
  depends=()

  cp --recursive "${srcdir}"/install-msw-${CARCH}-static${MSYSTEM_PREFIX}/ ${pkgdir}/
  cp --recursive "${srcdir}"/install-msw-${CARCH}${MSYSTEM_PREFIX}/ ${pkgdir}/

  mv ${pkgdir}${MSYSTEM_PREFIX}/lib/*.dll ${pkgdir}${MSYSTEM_PREFIX}/bin

  # Rename files
  mv ${pkgdir}${MSYSTEM_PREFIX}/bin/wx-config{,-${_wx_basever}}
  mv ${pkgdir}${MSYSTEM_PREFIX}/share/aclocal/wxwin.m4 ${pkgdir}${MSYSTEM_PREFIX}/share/aclocal/wxwin${_wx_basever}.m4

  # Add missing exe file extension (Likely bug in wxWidgets makefile)
  mv ${pkgdir}${MSYSTEM_PREFIX}/bin/wxrc-${_wx_basever} ${pkgdir}${MSYSTEM_PREFIX}/bin/wxrc-${_wx_basever}.exe

  # Remove conflicting files; that do not need to be renamed
  rm -f ${pkgdir}${MSYSTEM_PREFIX}/bin/wxrc.exe

  # Remove conflicting files; that I have no idea how to rename
  rm -fr ${pkgdir}${MSYSTEM_PREFIX}/share/bakefile
  rm -fr ${pkgdir}${MSYSTEM_PREFIX}/share/locale

  # Remove files
  rm -fr ${pkgdir}${MSYSTEM_PREFIX}/bin/wx-config

  # License files
  cd "${srcdir}"/${_sourcedir}/docs
  install -Dm644 preamble.txt "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_basename}${_wx_basever}/preamble.txt"
  install -Dm644 licence.txt  "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_basename}${_wx_basever}/licence.txt"
  install -Dm644 licendoc.txt "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_basename}${_wx_basever}/licendoc.txt"
  install -Dm644 lgpl.txt     "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_basename}${_wx_basever}/lgpl.txt"
  install -Dm644 gpl.txt      "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_basename}${_wx_basever}/gpl.txt"
  install -Dm644 xserver.txt  "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_basename}${_wx_basever}/xserver.txt"
}

package_wxmsw3.1-git() { _package_wxmsw; }
